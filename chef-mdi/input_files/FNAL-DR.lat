! Original :J.T. Rogers  1 Mar 2004  FILE: large.4.lat 
! Modified by A. Xiao 8 Mar 2004
! Maximum gradient 29.6T/m, should change quad length at wiggler section later


option, -echo
TITLE,"TESLA ALTERNATIVE DR LARGE RING";
BEAM,PARTICLE=ELECTRON,ENERGY=5.0 , RADIATE
RFC: RFCAVITY,L=0.5, VOLT=1, LAG=0.5,HARMON=7200 !500MHz, 10 cavities

!
! Bending magnets 
!
LENGTH := 4.3636;
ANGLE :=TWOPI/120;
B: SBEND, L=LENGTH, ANGLE=ANGLE, K1=0.0, E1=0.0, E2=0.0;
B2: SBEND, L=LENGTH/2.0, ANGLE=ANGLE/2.0, K1=0.0, E1=0.0, E2=0.0;

!
! Add-on's
!
HTLEN = 0.2
VTLEN = 0.2
HT: HKICK, L= HTLEN, KICK= 0.0
VT: VKICK, L= VTLEN, KICK= 0.0 

!no wiggler section
!qffake: multipole, k1l=-1.053136E-04, type=xtun
!qdfake: multipole, k1l= 0.991553E-04, type=ytun
!with wiggler section
qffake: multipole, k1l=-0.621703E-04, type=xtun
qdfake: multipole, k1l= 0.342166E-04, type=ytun

LBPM = 0.0
HPD: HMONITOR, TYPE = KHKD, L=LBPM
VPD: VMONITOR, TYPE = KVKD, L=LBPM

!********* wiggler section from NLC ****************
! wigglers
! (NOTE: nominal 21.5 kG wiggler peak field increased by ~6% to give correct
!        integrated By^2 and vertical focusing; nonzero K1 gives correct
!        horizontal focusing)
KE = 5.0
E0 = 0.000511 ! GeV
p0 = SQRT(KE*(2*E0+KE))    !3 GeV
BRH = p0/.2997925  !units of T-m
BRHO = 10*brh
 LAMW   = 0.27
 BW0    = 21.5
 BW     = 1.059*BW0
 THETAW = (BW*(LAMW/4))/Brho
 GW     = 0.009
 HLF    = 0
 WIGK1  = -0.0168
  BWPU : SBEND, L=LAMW/8, ANGLE=+THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=+THETAW/2, E2=0, FINT=HLF, FINTX=0
  BWPD : SBEND, L=LAMW/8, ANGLE=+THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=0, E2=+THETAW/2, FINT=0, FINTX=HLF
  BWMU : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=-THETAW/2, E2=0, FINT=HLF, FINTX=0
  BWMD : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=0, E2=-THETAW/2, FINT=0, FINTX=HLF
  BWPH : SBEND, L=LAMW/8, ANGLE=+THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=+THETAW/4, E2=+THETAW/4, FINT=HLF, FINTX=HLF
  BWMH : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=-THETAW/4, E2=-THETAW/4, FINT=HLF, FINTX=HLF

  WOCT : MULT, K3L=+100 !per A. Wolski model

  KQFW  :=  2.963063*0.3         ! 29.6 T/m
  KQDW  := -2.448550*0.3         ! 25   T/m
  QFWH  : QUAD, L=0.5/2, K1=KQFW
  QDWH  : QUAD, L=0.5/2, K1=KQDW
  
  L1W  := 0.3
  L2W  := LAMW/4
  L3W  := LAMW/16

  D1W  : DRIFT, L=L1W
  D1WT : DRIFT, L=L1W-HTLEN
  D2W  : DRIFT, L=L2W
  D3W  : DRIFT, L=L3W
  DWIG : DRIFT, L=2.52375-HTLEN 
  DINJ : DRIFT, L=11.595-0.5

  QFWU   : LINE = ( QFWH)
  QFWD   : LINE = ( QFWH, HPD, HT)  
  QDW    : LINE = ( QDWH, QDWH, VPD, VT)
  BWP    : LINE = ( BWPU, WOCT, BWPD)
  BWM    : LINE = ( BWMU, BWMD)
  WIGPRD : LINE = ( BWMD, D2W, BWP, D2W, BWMU, WOCT)
  WIGSEC : LINE = ( BWPH, D3W, BWMU, 6*WIGPRD, BWMD, D2W, BWP, D3W, BWMH)

  CWIG   : LINE = ( QFWD, D1WT, WIGSEC, D1W, QDW, D1WT, WIGSEC, D1W, QFWU)

CWIG0  : LINE = ( QFWD, DWIG, QDW, DWIG, QFWU)

QFIH  : QUAD, L=0.5/2, K1= 2.002893E-01
QDIH  : QUAD, L=0.5/2, K1=-2.002893E-01
QFIU   : LINE = ( QFIH)
QFID   : LINE = ( QFIH, HPD, HT)  
QDI    : LINE = ( QDIH, QDIH, VPD, VT)

CINJ   : LINE = ( QFIH, DINJ, rfc, QDIH, QDIH, DINJ, rfc,QFIH)

!
! Arc Section
!
QFAH: QUADRUPOLE, L=0.5/2, K1=2.135329E-01;
QDAH: QUADRUPOLE, L=0.5/2, K1=-1.741976E-01;
! no wiggler section
!SF:  SEXTUPOLE, L=0.25, K2=1.693998E+00 
!SD:  SEXTUPOLE, L=0.25, K2=-9.710336E-01
! with wiggler section
SF:  SEXTUPOLE, L=0.25, K2=0.0 !1.703742E+00 
SD:  SEXTUPOLE, L=0.25, K2=0.0 !-9.787570E-01
SMF:  SEXTUPOLE, L=0.25, K2=SF[K2];
SMD:  SEXTUPOLE, L=0.25, K2=SD[K2];
DA1T:DRIFT, L=11.3807-HTLEN;
DA2: DRIFT, L=0.25;
DA3T:DRIFT, L=1.8-HTLEN;
DA4: DRIFT, L=4.9;

QFA  : LINE = ( qffake, QFAH, QFAH, qffake, HPD, HT)
QDA  : LINE = ( qdfake, QDAH, QDAH, qdfake, VPD, VT)

CARC : LINE = ( B2, DA1T, -QDA, DA2, SD, DA3T, -QFA, DA4, SF, &
                DA4, QFA, DA3T, SD, DA2, QDA, DA1T, B2);
!
DM1: DRIFT, L=6.8;
DM2: DRIFT, L=2.3;
DM3: DRIFT, L=3.96;
DM4: DRIFT, L=5.32;

! Match to inject section 
! rf inside in this section
QMF_IA:    QUADRUPOLE,  L=0.5,  K1=  5.952844E-01   
QMD2_IA:   QUADRUPOLE,  L=0.5,  K1= -6.072457E-01   
QMD1_IA:   QUADRUPOLE,  L=0.5,  K1= -5.683965E-01 
QMF01_IA: QUADRUPOLE, L=0.5, K1=2.186321E-01;
QMD01_IA: QUADRUPOLE, L=0.5, K1=-1.918142E-01 ;
QMF02_IA: QUADRUPOLE, L=0.5, K1=2.362764E-01;
QMD02_IA: QUADRUPOLE, L=0.5, K1=-2.129177E-01;

QMF_I:    LINE = ( qffake, QMF_IA, qffake, HPD, HT)    
QMD2_I:   LINE = ( qdfake, QMD2_IA, qdfake, VPD, VT)  
QMD1_I:   LINE = ( qdfake, QMD1_IA, qdfake, VPD, VT)
QMD02_I:  LINE = ( qdfake, QMD02_IA, qdfake, VPD, VT) 
QMD01_I:  LINE = ( qdfake, QMD01_IA, qdfake, VPD, VT) 
QMF02_I:  LINE = ( qffake, QMF02_IA, qffake, HPD, HT)  
QMF01_I:  LINE = ( qffake, QMF01_IA, qffake, HPD, HT)
D1: DRIFT, L=14.443
MATI: LINE = (B2, DA1T, -QMD02_I,  DA2, SMD, DA3T, -QMF02_I, &
                DA4, SMF, DA4, QMF01_I, DA3T, &
                SMD, DA2, QMD01_I, D1, B2, & 
                DM1, QMD1_I, DM2, &
                QMF_I, DM3, QMD2_I, DM4,QFIH);

!
! Match to wiggler section
!
QMF_WA:    QUADRUPOLE,  L=0.5,  K1=  6.893998E-01   
QMD2_WA:   QUADRUPOLE,  L=0.5,  K1= -8.450721E-01    
QMD1_WA:   QUADRUPOLE,  L=0.5,  K1= -5.231142E-01 
QMD02_WA:  QUADRUPOLE,  L=0.5,  K1= -1.769090E-01  
QMD01_WA:  QUADRUPOLE,  L=0.5,  K1= -1.877992E-01   
QMF02_WA:  QUADRUPOLE,  L=0.5,  K1= 2.125098E-01 
QMF01_wA:  QUADRUPOLE,  L=0.5,  K1= 2.227099E-01

QMF_W:    LINE = ( qffake, QMF_WA, qffake, HPD, HT)    
QMD2_W:   LINE = ( qdfake, QMD2_WA, qdfake, VPD, VT)  
QMD1_W:   LINE = ( qdfake, QMD1_WA, qdfake, VPD, VT)
QMD02_W:  LINE = ( qdfake, QMD02_WA, qdfake, VPD, VT) 
QMD01_W:  LINE = ( qdfake, QMD01_WA, qdfake, VPD, VT) 
QMF02_W:  LINE = ( qffake, QMF02_WA, qffake, HPD, HT)  
QMF01_W:  LINE = ( qffake, QMF01_WA, qffake, HPD, HT)

MATW: LINE = (B2, DA1T, -QMD02_w,  DA2, SMD, DA3T, -QMF02_w,  &
              DA4, SMF, DA4, QMF01_w, DA3T, SMD, &
              DA2, QMD01_w, D1, B2, & 
              DM1, QMD1_w,  DM2, &
              QMF_w, DM3, QMD2_w, DM4, QFWU);




ARC:   LINE = ( 18*CARC );
WIG:   LINE = ( 10*CWIG );
INJ:   LINE = ( 4*CINJ );

RING:    LINE=(ARC, MATW,  3*CWIG0, WIG, 3*CWIG0, -MATW,  & 
               ARC, MATI,  INJ, -MATI, &
               ARC, MATI,  INJ, -MATI, &
               ARC, MATW,  3*CWIG0, WIG, 3*CWIG0, -MATW,  &
               ARC, MATI,  INJ, -MATI, &
               ARC, MATI,  INJ, -MATI)
RING0:    LINE=(ARC, MATI,  INJ, -MATI,  & 
               ARC, MATI,  INJ, -MATI, &
               ARC, MATI,  INJ, -MATI, &
               ARC, MATI,  INJ, -MATI, &
               ARC, MATI,  INJ, -MATI, &
               ARC, MATI,  INJ, -MATI)
RETURN