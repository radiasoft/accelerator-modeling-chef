! ==============================================================================
! TME Damping Ring
! ------------------------------------------------------------------------------
! 06-FEB-2003, M. Woodley
!    From Andy Wolski's MAD deck; reverse polarity of wiggler and straight
!    section FODOs; fix matching into FODOs; add octupole to wigglers; set up
!    optics for injection/extraction kickers and septa; make space for RF
!    cavities and circumference correction chicane
! 31-JAN-2003, M. Woodley
!    Increase length of arc QF ... try to keep quad pole-tip fields below
!    8 kG @ 25 mm radius (|K1|<4.845)
! ------------------------------------------------------------------------------

! injected beam

  EMITXNi := 1.5E-4 !normalized horizontal emittance (m)
  EMITYNi := 1.5E-4 !normalized vertical emittance (m)

! extracted beam

  EMITXNe := 3.0E-6   !normalized horizontal emittance (m)
  EMITYNe := 2.0E-8   !normalized vertical emittance (m)
  BLENGe  := 3.66E-3  !bunch length (m)
  ESPRDe  := 0.909E-3 !energy spread (1)

! global parameters

  E0    := 1.98              !beam energy (GeV)
  Cb    := 1.0e10/CLIGHT     !energy to rigidity (kG-m/GeV)
  Brho  := Cb*E0             !magnetic rigidity (kG-m)
  frf   := 714.0E6           !ring RF frequency (Hz)
  Nhar  := 714               !ring RF harmonic number
  Circ  := Nhar*(CLIGHT/frf) !ring circumference (path length)

! matching parameters

  mux0  := 0.445  !horizontal phase advance of arc TME cell (rad/2pi)
  muy0  := 0.118  !vertical phase advance of arc TME cell (rad/2pi)
  betx0 := 0.52   !horizontal beta at center of arc TME cell dipole (m)
  bety0 := 8.45   !vertical beta at center of arc TME cell dipole (m)
  dx0   := 0.0561 !horizontal dispersion at center of arc TME cell dipole (m)

  muxmw  := 0.238 !horizontal phase advance of wiggler FODO cell (rad/2pi)
  muymw  := 0.115 !vertical phase advance of wiggler FODO cell (rad/2pi)
  betxmw := 2.4   !horizontal beta at center of wiggler FODO QD (m)
  betymw := 10.2  !vertical beta at center of wiggler FODO QD (m)

  muxms  := 0.124 !horizontal phase advance of straight FODO cell (rad/2pi)
  muyms  := 0.386 !vertical phase advance of straight FODO cell (rad/2pi)
  betxms := 3.1   !horizontal beta at center of straight FODO QD (m)
  betyms := 11.3  !vertical beta at center of straight FODO QD (m)

  muxr   := 21.15  !horizontal tune (rad/2pi)
  muyr   := 10.347 !vertical tune (rad/2pi)
  dmuxwi := 2.886  !wiggler horizontal arc-to-arc phase advance (rad/2pi)
  dmuywi := 1.878  !wiggler vertical arc-to-arc phase advance (rad/2pi)
  dmuxsi := 1.417  !straight horizontal arc-to-arc phase advance (rad/2pi)
  dmuysi := 2.150  !straight vertical arc-to-arc phase advance (rad/2pi)

! ==============================================================================
! RF
! ------------------------------------------------------------------------------

  Lrf   := 0.6 !length of RF cavity (m)
  Vrf   := 2.0 !total RF voltage per turn (MV)
  rf_on := 1   !turn RF on (1) or off (0)

  RFC : RFCAV, L=Lrf, VOLT=rf_on*(Vrf/5), LAG=0.5, HARMON=714

! ==============================================================================
! BENDs
! ------------------------------------------------------------------------------
! QF   : off-axis arc quadrupole
! QFM  : off-axis dispersion suppressor quadrupole
! BU   : upstream half of combined-function arc dipole
! BD   : downstream half of combined-function arc dipole
! BMU  : upstream half of combined-function dispersion suppressor dipole
! BMD  : downstream half of combined-function dispersion suppressor dipole
! BWP  : full wiggler pole (bend +) 
! BWM  : full wiggler pole (bend -)
! BWPH : half wiggler pole (bend +)
! BWMH : half wiggler pole (bend -)
! BWMU : upstream half of full wiggler pole (bend -)
! BWMD : downstream half of full wiggler pole (bend -)
! ------------------------------------------------------------------------------

! arcs: off-axis quads

  LQF  := 0.25
  AQF  := -0.272929109825E-2
  KQF  := 4.197537926742
  KQFM := 4.601623909036

  QF  : SBEND, L=LQF/2, ANGLE=AQF/2, K1=KQF
  QFM : SBEND, L=LQF/2, ANGLE=AQF/2, K1=KQFM

! arcs: combined-function dipoles

  LB  := 2.0
  LBM := 1.8
  AB  := 2*PI/32-2*AQF
  KB  := -0.3152733
  KBM := -0.67120058233

  BU  : SBEND, L=LB/2, ANGLE=AB/2, K1=KB, &
               HGAP=0, E1=AB/2, E2=0, FINT=0, FINTX=0
  BD  : SBEND, L=LB/2, ANGLE=AB/2, K1=KB, &
               HGAP=0, E1=0, E2=AB/2, FINT=0, FINTX=0
  BMU : SBEND, L=LBM/2, ANGLE=AB/4, K1=KBM, &
               HGAP=0, E1=AB/4, E2=0, FINT=0, FINTX=0
  BMD : SBEND, L=LBM/2, ANGLE=AB/4, K1=KBM, &
               HGAP=0, E1=0, E2=AB/4, FINT=0, FINTX=0

! wigglers
! (NOTE: nominal 21.5 kG wiggler peak field increased by ~6% to give correct
!        integrated By^2 and vertical focusing; nonzero K1 gives correct
!        horizontal focusing)

  LAMW   := 0.27
  BW0    := 21.5
  BW     := 1.059*BW0
  THETAW := (BW*(LAMW/4))/Brho
  GW     := 0.009
  HLF    := 0
  WIGK1  := -0.0168

  BWPU : SBEND, L=LAMW/8, ANGLE=+THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=+THETAW/2, E2=0, FINT=HLF, FINTX=0
  BWPD : SBEND, L=LAMW/8, ANGLE=+THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=0, E2=+THETAW/2, FINT=0, FINTX=HLF
  BWMU : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=-THETAW/2, E2=0, FINT=HLF, FINTX=0
  BWMD : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=0, E2=-THETAW/2, FINT=0, FINTX=HLF
  BWPH : SBEND, L=LAMW/8, ANGLE=+THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=+THETAW/4, E2=+THETAW/4, FINT=HLF, FINTX=HLF
  BWMH : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1, &
                HGAP=GW, E1=-THETAW/4, E2=-THETAW/4, FINT=HLF, FINTX=HLF

  WOCT : MULT, K3L=+100 !per A. Wolski model

! circumference correction chicane

  BBC  := -4.9228           !B-field (kG)
  RBC  := Brho/BBC          !bend radius (m)
  LBC  := 0.6               !effective length (m)
  ABC  := LBC/RBC           !bend angle (rad)
  ZBC  := RBC*SIN(ABC)      !"Z" length (m)
  ABC0 := ASIN((ZBC/2)/RBC) !"short" half chicane bend angle (rad)
  LBC0 := RBC*ABC0          !"short" half chicane bend path length (m)
  ABC1 := ABC-ABC0          !"long" half chicane bend angle (rad)
  LBC1 := RBC*ABC1          !"long" half chicane bend path length (m)
  GBC  := 0.04              !gap height (m)

  BC10 : SBEN, L=LBC0, ANGLE=+ABC0, HGAP=GBC/2, E1=0, FINT=0.5, FINTX=0
  BC11 : SBEN, L=LBC1, ANGLE=+ABC1, HGAP=GBC/2, E2=+ABC, FINT=0, FINTX=0.5
  BC21 : SBEN, L=LBC1, ANGLE=-ABC1, HGAP=GBC/2, E1=-ABC, FINT=0.5, FINTX=0
  BC20 : SBEN, L=LBC0, ANGLE=-ABC0, HGAP=GBC/2, E2=0, FINT=0, FINTX=0.5
  BC30 : SBEN, L=LBC0, ANGLE=-ABC0, HGAP=GBC/2, E1=0, FINT=0.5, FINTX=0
  BC31 : SBEN, L=LBC1, ANGLE=-ABC1, HGAP=GBC/2, E2=-ABC, FINT=0, FINTX=0.5
  BC41 : SBEN, L=LBC1, ANGLE=+ABC1, HGAP=GBC/2, E1=+ABC, FINT=0.5, FINTX=0
  BC40 : SBEN, L=LBC0, ANGLE=+ABC0, HGAP=GBC/2, E2=0, FINT=0, FINTX=0.5

! bend-to-bend path lengths

  ZBBo := ZBC           !outer bend-to-bend "Z" spacing (m)
  LBBo := ZBBo/COS(ABC) !outer bend-to-bend drift length (m)
  LBBi := 0.2           !inner bend-to-bend "Z" spacing (m)

value,LBBo
value,ABC0
value,ABC1

  DBBo : DRIF, L=LBBo
  DBBi : DRIF, L=LBBi/2

! (path length - "Z" length) for chicane

  LCC := 4*LBC+2*LBBo+LBBi
  ZCC := 4*ZBC+2*ZBBo+LBBi
  dCC := LCC-ZCC

! injection/extraction

  Lkik  := 1.20  ! length of kicker magnet (m)
  Lsep1 := 0.83  ! length of 1st inj/ext septum magnet (m)
  Lsep2 := 1.00  ! length of 2nd inj/ext septum magnet (m)

  IKIK  : SBEND, L=Lkik/2
  ISEP1 : SBEND, L=Lsep1/2
  ISEP2 : SBEND, L=Lsep2/2
  EKIK  : SBEND, L=Lkik/2
  ESEP1 : SBEND, L=Lsep1/2
  ESEP2 : SBEND, L=Lsep2/2

! ==============================================================================
! QUADs
! ------------------------------------------------------------------------------
! QD        : arc
! QFW,QDW   : wiggler FODO
! Q1WM-Q4WM : wiggler matching
! Q1S-Q7S   : injection/extraction straight
! ------------------------------------------------------------------------------

  Q15 : QUAD, L=0.15/2, APER=0.025
  Q20 : QUAD, L=0.2/2, APER=0.025
  Q25 : QUAD, L=0.25/2, APER=0.025

  KQD := -4.453197083612

  KQ1WM :=  4.0806989025
  KQ2WM := -4.658438664475
  KQ3WM :=  3.17147061472
  KQ4WM := -1.217105159825
  KQFW  :=  3.213836342096
  KQDW  := -1.210600407874

  KQ1I :=  4.55535014238
  KQ2I := -4.541881449966
  KQ3I :=  1.822255934944
  KQ4I := -2.151528549977
  KQ5I :=  1.725612827226
  KQ6I := -4.191807601354
  KQ7I :=  4.642316951637

  KQ1E :=  4.648067151231
  KQ2E := -4.203383711248
  KQ3E :=  1.721808212935
  KQ4E := -2.138753072255
  KQ5E :=  1.821152433453
  KQ6E := -4.544424164816
  KQ7E :=  4.559543114155

  QD : Q20, K1=KQD

  Q1WM : Q25, K1=KQ1WM
  Q2WM : Q15, K1=KQ2WM
  Q3WM : Q15, K1=KQ3WM
  Q4WM : Q15, K1=KQ4WM
  QFW  : Q15, K1=KQFW
  QDW  : Q15, K1=KQDW

  Q1I : Q20, K1=KQ1I
  Q2I : Q20, K1=KQ2I
  Q3I : Q20, K1=KQ3I
  Q4I : Q20, K1=KQ4I
  Q5I : Q20, K1=KQ5I
  Q6I : Q20, K1=KQ6I
  Q7I : Q20, K1=KQ7I

  Q1E : Q20, K1=KQ1E
  Q2E : Q20, K1=KQ2E
  Q3E : Q20, K1=KQ3E
  Q4E : Q20, K1=KQ4E
  Q5E : Q20, K1=KQ5E
  Q6E : Q20, K1=KQ6E
  Q7E : Q20, K1=KQ7E

! ==============================================================================
! SEXTs
! ------------------------------------------------------------------------------

  sext_on := 1
  S10 : SEXT, L=0.1/2

  KSF :=  1.646748299323E2 !*1.005
  KSD := -1.573696044655E2 !*0.99

  SF : S10, K2=KSF*sext_on
  SD : S10, K2=KSD*sext_on

! ==============================================================================
! DRIFTs
! ------------------------------------------------------------------------------

  L1A := 0.08
  L1B := 0.3749
  L1C := 0.08
  L2  := 0.3692

  D1A : DRIFT, L=L1A
  D1B : DRIFT, L=L1B
  D1C : DRIFT, L=L1C
  D2  : DRIFT, L=L2

  L1W  := 0.3
  L2W  := LAMW/4
  L3W  := LAMW/16
  L1WM := 0.55526409933
  L2WM := 1.44156712905

  D1W  : DRIFT, L=L1W
  D2W  : DRIFT, L=L2W
  D3W  : DRIFT, L=L3W
  D1WM : DRIFT, L=L1WM
  D2WM : DRIFT, L=L2WM

  LSQ   := 0.1                   !septum-to-quad spacing (m)
  LKQ   := 0.1                   !kicker-to-quad spacing (m)
  LRR   := 2.75*(CLIGHT/frf)-Lrf !cavity-to-cavity spacing (m)
  LRQ   := (LRR-0.2)/2           !cavity-to-quad spacing (m)
  LCIRC := 1.80397102853         !adjust to correct circumference (m)

  DSQ : DRIFT, L=LSQ
  DKQ : DRIFT, L=LKQ
  DRR : DRIFT, L=LRR
  DRQ : DRIFT, L=LRQ

  dL1I := 0
  dL2I := 0
  dL3I := 0
  dL4I := 0
  L1I  := 0.912141519872-dL4I
  L2I  := 1.141758105384+dL4I
  L3I  := LCIRC+0.010831561196-dL1I-dL2I
  L4I  := 3.418061647845+dL2I-dL3I
  L5I  := 2.252051741432+dL3I
  L6I  := (4*Lrf+3*LRR+2*LRQ)+dL1I
  L7I  := 1.141758105384+dL4I
  L8I  := 0.912141519872-dL4I

  D1I : DRIFT, L=L1I
  D2I : DRIFT, L=L2I
  D3I : DRIFT, L=L3I
  D4I : DRIFT, L=L4I
  D5I : DRIFT, L=L5I
  D6I : DRIFT, L=L6I
  D7I : DRIFT, L=L7I
  D8I : DRIFT, L=L8I

  dL1E := 0
  dL2E := 0
  dL3E := 0
  dL4E := 0
  L1E  := 0.908301505744-dL4E
  L2E  := 1.145598119512+dL4E
  L3E  := (4*Lrf+3*LRR+2*LRQ)+dL1E
  L4E  := 2.262494757574+dL3E
  L5E  := 3.418450066326+dL2E-dL3E
  L6E  := LCIRC+0.126572782255E-6-dL1E-dL2E
  L7E  := 1.145598119512+dL4E
  L8E  := 0.908301505744-dL4E

  D1E : DRIFT, L=L1E
  D2E : DRIFT, L=L2E
  D3E : DRIFT, L=L3E
  D4E : DRIFT, L=L4E
  D5E : DRIFT, L=L5E
  D6E : DRIFT, L=L6E
  D7E : DRIFT, L=L7E
  D8E : DRIFT, L=L8E

  LCQ := (L6I-LCC)/2
  L1R := L2E-(Lrf+LRQ)

  DCQ : DRIFT, L=LCQ
  D1R : DRIFT, L=L1R

! ==============================================================================
! MARKs
! ------------------------------------------------------------------------------

  RINJ1 : MARK
  RINJ2 : MARK
  REXT1 : MARK
  REXT2 : MARK

! ==============================================================================
! BEAMLINEs
! ------------------------------------------------------------------------------

  CELLU    : LINE=(BD,D2,QF,QF,D1C,SF,SF,D1B,SD,SD,D1A,QD)
  CELLD    : LINE=(QD,D1A,SD,SD,D1B,SF,SF,D1C,QF,QF,D2,BU)
  ARCCELL  : LINE=(CELLU,CELLD)
  ARCCELLR : LINE=(CELLD,CELLU)
  SUPPU    : LINE=(BMU,BMD,D2,QFM,QFM,D1C,SF,SF,D1B,SD,SD,D1A,QD)
  SUPPD    : LINE=(QD,D1A,SD,SD,D1B,SF,SF,D1C,QFM,QFM,D2,BMU,BMD)

  BWP      : LINE=(BWPU,WOCT,BWPD)
  BWM      : LINE=(BWMU,BWMD)
  WIGPRD   : LINE=(BWMD,D2W,BWP,D2W,BWMU,WOCT)
  WIGSEC   : LINE=(BWPH,D3W,BWMU,6*WIGPRD,BWMD,D2W,BWP,D3W,BWMH)
  CELLW    : LINE=(QFW,D1W,WIGSEC,D1W,QDW,QDW,D1W,WIGSEC,D1W,QFW)
  CELLMWU  : LINE=(SUPPD,D1WM,Q1WM,Q1WM,D2WM,Q2WM,Q2WM,&
                   D1W,WIGSEC,D1W,Q3WM,Q3WM,D1W,WIGSEC,D1W,Q4WM,Q4WM,&
                   D1W,WIGSEC,D1W,QFW)
  CELLMWD  : LINE=(QFW,D1W,WIGSEC,D1W,&
                   Q4WM,Q4WM,D1W,WIGSEC,D1W,Q3WM,Q3WM,D1W,WIGSEC,D1W,&
                   Q2WM,Q2WM,D2WM,Q1WM,Q1WM,D1WM,SUPPU)
  WIGINS   : LINE=(CELLMWU,5*CELLW,CELLMWD)

  CHICANE  : LINE=(BC10,BC11,DBBo,BC21,BC20,2*DBBi,BC30,BC31,DBBo,BC41,BC40)
  D6IC     : LINE=(DCQ,CHICANE,DCQ)
  STRINJ   : LINE=(SUPPD,D1I,Q1I,Q1I,D2I,Q2I,Q2I,D3I,ISEP2,ISEP2,DSQ,&
                   Q3I,Q3I,DSQ,ISEP1,ISEP1,RINJ1,D4I,Q4I,Q4I,D5I,IKIK,RINJ2,&
                   IKIK,DKQ,Q5I,Q5I,D6IC,Q6I,Q6I,D7I,Q7I,Q7I,D8I,SUPPU)
  STRINJU  : LINE=(SUPPD,D1I,Q1I,Q1I,D2I,Q2I,Q2I,D3I,ISEP2,ISEP2,DSQ,&
                   Q3I,Q3I,DSQ,ISEP1,ISEP1,RINJ1,D4I,Q4I,Q4I,D5I,IKIK,RINJ2,&
                   IKIK,DKQ,Q5I)
  STRINJD  : LINE=(Q5I,D6IC,Q6I,Q6I,D7I,Q7I,Q7I,D8I,SUPPU)

  D2ER     : LINE=(D1R,RFC,DRQ)
  D3ER     : LINE=(DRQ,RFC,DRR,RFC,DRR,RFC,DRR,RFC,DRQ)
  EXTRACTOR: LINE=(D4E,Q4E,Q4E,D5E,REXT2)
  STREXT   : LINE=(SUPPD,D1E,Q1E,Q1E,D2E,Q2E,Q2E,D3E,Q3E,Q3E,&
                   DKQ,EKIK,REXT1,EKIK,D4E,Q4E,Q4E,D5E,REXT2,ESEP1,ESEP1,DSQ,&
                   Q5E,Q5E,DSQ,ESEP2,ESEP2,D6E,Q6E,Q6E,D7E,Q7E,Q7E,D8E,SUPPU)
  STREXTR  : LINE=(SUPPD,D1E,Q1E,Q1E,D2ER,Q2E,Q2E,D3ER,Q3E,Q3E,&
                   DKQ,EKIK,REXT1,EKIK,D4E,Q4E,Q4E,D5E,REXT2,ESEP1,ESEP1,DSQ,&
                   Q5E,Q5E,DSQ,ESEP2,ESEP2,D6E,Q6E,Q6E,D7E,Q7E,Q7E,D8E,SUPPU)

  RING     : LINE=(6*ARCCELL,CELLU,WIGINS,CELLD,&
                   6*ARCCELL,CELLU,STRINJ,CELLD,&
                   6*ARCCELL,CELLU,WIGINS,CELLD,&
                   6*ARCCELL,CELLU,STREXT,CELLD)
  RINGR    : LINE=(6*ARCCELL,CELLU,WIGINS,CELLD,&
                   6*ARCCELL,CELLU,STRINJ,CELLD,&
                   6*ARCCELL,CELLU,WIGINS,CELLD,&
                   6*ARCCELL,CELLU,STREXTR,CELLD)

  RINGINJ  : LINE=(STRINJD,CELLD,&
                   6*ARCCELL,CELLU,WIGINS,CELLD,&
                   6*ARCCELL,CELLU,STREXT,CELLD,&
                   6*ARCCELL,CELLU,WIGINS,CELLD,&
                   6*ARCCELL,CELLU,STRINJU)
  RINGINJR : LINE=(STRINJD,CELLD,&
                   6*ARCCELL,CELLU,WIGINS,CELLD,&
                   6*ARCCELL,CELLU,STREXTR,CELLD,&
                   6*ARCCELL,CELLU,WIGINS,CELLD,&
                   6*ARCCELL,CELLU,STRINJU)

! ==============================================================================
beam, particle=positron, energy=1.98, radiate
use, ringr
twiss, save
PLOT, HAXIS=s, STYLE = 100 , SPLINE, colour=100, &
      VAXIS1 = BETX, BETY, VAXIS2 =DX, hmin=0, hmax=50
emit
!bmpm
   RETURN

