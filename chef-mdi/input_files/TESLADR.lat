! Linear lattice for Tesla's smaller damping ring 
! Creat by A. Xiao, June 16, 2004
!
! Beam Energy E0 = 5.066GeV to keep spin tune a*gamma=11.5
! Cicumference C = 6113.967388 to accomodate 2820 bunches
! June 27, 2004   Add sextupole to the ring
 
option, -echo

TITLE,"TESLA SMALL DAMPING RING"

BEAM,PARTICLE=ELECTRON,ENERGY=5.066 , RADIATE
RFC: RFCAVITY,L=0.5, VOLT=0.63, LAG=0.5,HARMON=10197  !500MHz, 12 cavities
E0:  = 5.066

!MARKER
M0: MARKER
!
! Arc section 
!
TWOPI  := 6.2831853
LENGTH := 5.6
BANGLE :=TWOPI/72

BA:   RBEND, L=LENGTH,   ANGLE=BANGLE,   K1=0.0, E1=0.0, E2=0.0
BA2:  RBEND, L=LENGTH/2, ANGLE=BANGLE/2, K1=0.0, E1=0.0, E2=0.0

QFA:  QUADRUPOLE, L=0.25, K1=5.996136E-01
QDA:  QUADRUPOLE, L=0.25, K1=-5.267278E-01

SF:  SEXTUPOLE, L=0.25, K2=  1.371835E+00 !1.708621E+00
SD:  SEXTUPOLE, L=0.25, K2= -2.033596E+00 !-2.644487E+00

DLA1:  DRIFT, L=9.173
DSF:   DRIFT, L=0.2
DLA2:  DRIFT, L=2.007
DSD:   DRIFT, L=0.2
DLA3:  DRIFT, L=5.934

ACELL0: LINE=(DLA1, SF, DSF, QFA, DLA2, QDA, DSD, SD, DLA3, &
              BA, DLA3, SD, DSD, QDA, DLA2, QFA, DSF, SF, DLA1)
ACELL:  LINE=(BA2, DLA3, SD, DSD, QDA, DLA2, QFA, DSF, SF, 2*DLA1, &
              SF, DSF, QFA, DLA2, QDA, DSD, SD, DLA3, BA2)

!
! Straight section
!				
QFI:    QUADRUPOLE, L=0.25/2,   K1= 3.018740E-01
QDI:    QUADRUPOLE, L=0.25/2,   K1=-3.026188E-01

DLI1:   DRIFT, L=11.75
SCELL0: LINE=( QDI, DLI1, QFI, QFI, DLI1, QDI)

! Dispersion suppressor
DLMA1:   DRIFT, L=8.892655
DLMS1:   DRIFT, L=0.5
DLMS2:   DRIFT, L=2.142310   
DLMS3:   DRIFT, L=9.375134   
DLMS4:   DRIFT, L=29.788282-DLMS3[L]-DLMS2[L]-DLMS1[L]-DLMA1[L] !
 
QFMA1:   QUADRUPOLE, L=0.25, K1= 5.996136E-01
QDMA1:   QUADRUPOLE, L=0.25, K1=-5.267260E-01
QDMS1:   QUADRUPOLE, L=0.25, K1=-4.708655E-01
QFMS1:   QUADRUPOLE, L=0.25, K1= 7.219233E-01
QDMS2:   QUADRUPOLE, L=0.25, K1=-2.977210E-01
QFMS2:   QUADRUPOLE, L=0.25, K1= 3.617724E-01

SUP1:   LINE = ( BA2, DLA3, SD, DSD, QDA, DLA2, QFA, DSF, SF, & 
                 2*DLA1, SF, DSF, &
                 QFMA1, DLA2, QDMA1, DSD, SD, DLMA1, BA2, &
                 DLMS1, RFC,  DLMS1, QDMS1, DLMS2, &
                 QFMS1, DLMS3, QDMS2, DLMS4, QFMS2, DLI1, QDI) 


!Tune adjust section
QFT:    QUADRUPOLE, L=0.25/2,   K1= 3.353711E-01
QDT:    QUADRUPOLE, L=0.25/2,   K1=-3.337687E-01

SCELL1: LINE=( QDT, DLI1, QFT, QFT, DLI1, QDT)

QDMT1:   QUADRUPOLE, L=0.25, K1=-4.930717E-01
QFMT1:   QUADRUPOLE, L=0.25, K1= 7.408979E-01
QDMT2:   QUADRUPOLE, L=0.25, K1=-3.010138E-01
QFMT2:   QUADRUPOLE, L=0.25, K1= 3.797060E-01

SUPT:   LINE = ( BA2, DLA3, SD, DSD, QDA, DLA2, QFA, DSF, SF, & 
                 2*DLA1, SF, DSF, &
                 QFMA1, DLA2, QDMA1, DSD, SD, DLMA1, BA2, &
                 DLMS1, RFC,  DLMS1, QDMT1, DLMS2, &
                 QFMT1, DLMS3, QDMT2, DLMS4, QFMT2, DLI1, QDT) 

!********* wiggler section from NLC ****************
! wigglers
! (NOTE: nominal 21.5 kG wiggler peak field increased by ~6% to give correct
!        integrated By^2 and vertical focusing; nonzero K1 gives correct
!        horizontal focusing)
BRH = E0/.2997925  !units of T-m
BRHO = 10*brh
 LAMW   = 0.27
 BW0    = 21.5
 BW     = 1.059*BW0
 THETAW = (BW*(LAMW/4))/Brho
 GW     = 0.009
 HLF    = 0
 WIGK1  = -0.0168
  BWPU : SBEND, L=LAMW/8, ANGLE= THETAW/2, K1=WIGK1
!                HGAP=GW, E1= THETAW/2, E2=0, FINT=HLF, FINTX=0
  BWPD : SBEND, L=LAMW/8, ANGLE= THETAW/2, K1=WIGK1
!                HGAP=GW, E1=0, E2= THETAW/2, FINT=0, FINTX=HLF
  BWMU : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1
!                HGAP=GW, E1=-THETAW/2, E2=0, FINT=HLF, FINTX=0
  BWMD : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1
!                HGAP=GW, E1=0, E2=-THETAW/2, FINT=0, FINTX=HLF
  BWPH : SBEND, L=LAMW/8, ANGLE= THETAW/2, K1=WIGK1
!                HGAP=GW, E1= THETAW/4, E2= THETAW/4, FINT=HLF, FINTX=HLF
  BWMH : SBEND, L=LAMW/8, ANGLE=-THETAW/2, K1=WIGK1
!                HGAP=GW, E1=-THETAW/4, E2=-THETAW/4, FINT=HLF, FINTX=HLF

  WOCT : MULTIPOLE, K3L= 0 !+100 !per A. Wolski model

  KQFW  :=  0.8889189        !
  KQDW  := -0.734565         !
  QFWH  : QUADRUPOLE, L=0.5/2, K1=KQFW
  QDWH  : QUADRUPOLE, L=0.5/2, K1=KQDW
  
  L1W  := 0.3
  L2W  := LAMW/4
  L3W  := LAMW/16

  D1W  : DRIFT, L=L1W
  D2W  : DRIFT, L=L2W
  D3W  : DRIFT, L=L3W

  BWP    : LINE = ( BWPU, WOCT, BWPD)
  BWM    : LINE = ( BWMU, BWMD)
  WIGPRD : LINE = ( BWMD, D2W, BWP, D2W, BWMU, WOCT)
  WIGSEC : LINE = ( BWPH, D3W, BWMU, 6*WIGPRD, BWMD, D2W, BWP, D3W, BWMH)

  CWIG   : LINE = ( QFWH, D1W, WIGSEC, D1W, QDWH, QDWH, D1W, WIGSEC, D1W, QFWH)

!
! Match normal straight cell to wiggler cell
!
QFMW1:  QUADRUPOLE, L=0.25/2,   K1= 7.658104E-01
QDMW1:  QUADRUPOLE, L=0.25/2,   K1=-5.020842E-01
QDMW2:  QUADRUPOLE, L=0.25/2,   K1=-9.437328E-01
QFMW2:  QUADRUPOLE, L=0.25/2,   K1= 3.143134E-01
DLMW1:  DRIFT, L=6.828116 !6.660012
DLMW2:  DRIFT, L=6.418479 !6.821924
DLMW3:  DRIFT, L=16.8875-DLMW1[L]-DLMW2[L]
DLI1A:  DRIFT, L=(DLI1[L]-4)/2
STR_WIG: LINE = ( QDI, DLI1A, 8*RFC, DLI1A, QFMW2, QFMW2, DLI1, &
                  QDMW1, QDMW1, DLMW1, QFMW1, &
                  QFMW1, DLMW2, QDMW2, QDMW2, DLMW3, QFWH)

HARC:   LINE = (5*ACELL)
HARC1:   LINE = (6*ACELL)

SEXT0:  LINE = ( HARC, SUP1, 9*SCELL0, M0, 9*SCELL0, -SUP1, HARC)
SEXTW:  LINE = ( HARC, SUP1, 6*SCELL0, STR_WIG, 10*CWIG, &
                 -STR_WIG, 6*SCELL0, -SUP1, HARC)
SEXTT:  LINE = ( HARC, SUPT, 18*SCELL1, -SUPT, HARC)


RING0:  LINE = (6*SEXTT)
RINGW:  LINE = (2*SEXTT, SEXTW, 2*SEXTT, SEXTW)

