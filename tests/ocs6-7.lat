!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! File: OCS6_7.lat
!! ----------------
!! Variation #7 of OCS6.xsif.
!! No functional change, but a redefinition of lines:
!! + shifts cell boundaries to eliminate split magnets,
!!   esp. those at different tree levels.
!! + clarifies symmetries and parallel structures
!!   (I hope: esp. of straight sections).
!! + presents lines in a hierarchy of five levels.
!! 
!! Leo Michelotti
!! August 31, 2006.
!! 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
! ILC e+ Damping Ring Baseline Lattice
! Created by A. Xiao and L. Emery (ANL), 20 June 2006
! [[ Modified: see above. ]]
!
! Beam Energy E0 = 5.0 GeV 
! Cicumference C = 6695.057384 (for 650MHz RF, h=14516)
! Arc Cell Phase Advance 90/90
!

TWOPI     := 6.2831853071795864769
E0        := 5.0  ! Beam Energy [[ momentum? ]]


!--------1---------2---------3---------4---------5---------6---------7--------8
! RF Cavities
!--------1---------2---------3---------4---------5---------6---------7--------8

RF_ON     := 0
RFVOLT    := RF_ON*46.6/32

! 650MHz RF, 12 RF Cavities

RFC       :  RFCAVITY, L=0.23, VOLT=RFVOLT, LAG=0.5, HARMON=14516  


!--------1---------2---------3---------4---------5---------6---------7--------8
! Arc Section
!--------1---------2---------3---------4---------5---------6---------7--------8

DSF       :  DRIFT, L=0.2
DSD       :  DRIFT, L=0.2
DLA1      :  DRIFT, L=6.65
DLA2      :  DRIFT, L=3.6
DLA3      :  DRIFT, L=4.7

BANGLE    := TWOPI/120
HBANGLE   := BANGLE/2
QBANGLE   := BANGLE/4

BA        :  SBEND, L=6, ANGLE=BANGLE,  E1=HBANGLE, E2=HBANGLE
BA2       :  SBEND, L=3, ANGLE=HBANGLE, E1=QBANGLE, E2=QBANGLE

QFA       :  QUADRUPOLE, L=0.3, K1= 2.852939E-01
QDA       :  QUADRUPOLE, L=0.3, K1=-2.869627E-01

SXT_ON    := 1

SF        :  SEXTUPOLE,  L=0.25, K2= 0.622*SXT_ON
SD        :  SEXTUPOLE,  L=0.25, K2=-0.960*SXT_ON


!--------1---------2---------3---------4---------5---------6---------7--------8
! Straight Section
!--------1---------2---------3---------4---------5---------6---------7--------8

DLI1      :  DRIFT, L=13.26
DLI1H     :  DRIFT, L=6.63

QDI       :  QUADRUPOLE, L=0.3, K1= -2.349378E-01
QFI       :  QUADRUPOLE, L=0.3, K1=  2.558295E-01


!--------1---------2---------3---------4---------5---------6---------7--------8
! Wiggler Section
!--------1---------2---------3---------4---------5---------6---------7--------8

D1W       :  DRIFT, L=0.5
D1WM      :  DRIFT, L=0.125   ! = 0.5 - 0.375
D2W       :  DRIFT, L=0.1
D2WH      :  DRIFT, L=0.05
D3W       :  DRIFT, L=0.025

BWP       :  SBEND, L=0.10, ANGLE=2.0*(0.473418870904E-2), &
                   E1=0.473418870904E-2, E2=0.473418870904E-2
BWM       :  SBEND, L=0.10, ANGLE=2.0*(-0.473418870904E-2),&
                   E1=-0.473418870904E-2, E2=-0.473418870904E-2
BWPH      :  SBEND, L=0.05, ANGLE=0.473418870904E-2, &
                   E1=0.236709435452E-2, E2=0.236709435452E-2
BWMH      :  SBEND, L=0.05, ANGLE=-0.473418870904E-2,&
                   E1=-0.236709435452E-2, E2=-0.236709435452E-2

QDW       :  QUADRUPOLE, L=0.3,  K1= -5.956951E-01
QFW       :  QUADRUPOLE, L=0.3,  K1=  6.565377E-01


!--------1---------2---------3---------4---------5---------6---------7--------8
! RF Section
!--------1---------2---------3---------4---------5---------6---------7--------8

DRF       :  DRIFT, L = 0.75
DRFH      :  DRIFT, L = 0.375

QDRF      :  QUADRUPOLE, L=0.3, K1= -6.624226E-01
QFRF      :  QUADRUPOLE, L=0.3, K1=  7.015743E-01


!--------1---------2---------3---------4---------5---------6---------7--------8
! Dispersion Suppressor
!--------1---------2---------3---------4---------5---------6---------7--------8

DLB       :  DRIFT, L=6.0
DLS       :  DRIFT, L=4.06

QFMA1     :  QUADRUPOLE, L=0.3, K1= 3.318374E-01
QDMA1     :  QUADRUPOLE, L=0.3, K1=-2.638614E-01

SF1       :  SEXTUPOLE, L=0.25, K2=0.0
SD1       :  SEXTUPOLE, L=0.25, K2=0.0


!--------1---------2---------3---------4---------5---------6---------7--------8
! Matching to Wiggler
!--------1---------2---------3---------4---------5---------6---------7--------8

DLMA1     :  DRIFT, L=10.23 
DLMA2     :  DRIFT, L=11.252
DLMA3     :  DRIFT, L=10.8
DLMA4     :  DRIFT, L= 7.1
DLMT1     :  DRIFT, L= 3.5
DLMT2     :  DRIFT, L= 2.65
DLMT3     :  DRIFT, L=13.2
DLMT4     :  DRIFT, L= 8.0
DLMT5     :  DRIFT, L= 8.525  ! = 8.9  - 0.375


QFMA2     :  QUADRUPOLE, L=0.3, K1= 5.570872E-01
QDMA2     :  QUADRUPOLE, L=0.3, K1=-4.893358E-01
QFMA3     :  QUADRUPOLE, L=0.3, K1= 3.574218E-01
QDMA3     :  QUADRUPOLE, L=0.3, K1=-4.162628E-01
QDMT1     :  QUADRUPOLE, L=0.3, K1=-6.315772E-01
QFMT1     :  QUADRUPOLE, L=0.3, K1= 5.362670E-01
QDMT2     :  QUADRUPOLE, L=0.3, K1=-4.335431E-01
QFMT2     :  QUADRUPOLE, L=0.3, K1= 4.647991E-01


!--------1---------2---------3---------4---------5---------6---------7--------8
! Matching to Idle Straight 
!--------1---------2---------3---------4---------5---------6---------7--------8

DLMS1     :  DRIFT, L= 3
DLMS2     :  DRIFT, L= 1
DLMS3     :  DRIFT, L=15.733
DLMS4     :  DRIFT, L=5.905
DLMS5     :  DRIFT, L=4.442346  ! = 11.072346 - 6.63

QDMS1     :  QUADRUPOLE, L=0.3, K1=-4.471842E-01
QFMS1     :  QUADRUPOLE, L=0.3, K1= 3.490461E-01
QDMS2     :  QUADRUPOLE, L=0.3, K1=-2.932069E-01
QFMS2     :  QUADRUPOLE, L=0.3, K1= 2.357405E-01


!--------1---------2---------3---------4---------5---------6---------7--------8
! Matching to Injection
!--------1---------2---------3---------4---------5---------6---------7--------8

DMI1      :  DRIFT, L= 3
DMI2      :  DRIFT, L= 1
DMI3      :  DRIFT, L=15.685
DMI4      :  DRIFT, L= 5.884
DMI5      :  DRIFT, L=11.521346
DMI6      :  DRIFT, L=14.5

QDMINJ1   :  QUADRUPOLE, L=0.3, K1=-4.096847E-01
QFMINJ1   :  QUADRUPOLE, L=0.3, K1= 2.537282E-01
QDMINJ2   :  QUADRUPOLE, L=0.3, K1=-3.325470E-01
QFMINJ2   :  QUADRUPOLE, L=0.3, K1= 2.411528E-01
QDMINJ3   :  QUADRUPOLE, L=0.3, K1=-2.685610E-01
QFMINJ3   :  QUADRUPOLE, L=0.3, K1= 2.136230E-01


!--------1---------2---------3---------4---------5---------6---------7--------8
! Injection section 
!--------1---------2---------3---------4---------5---------6---------7--------8

! Fast strip line kicker
! Theta = 2*10E3(Voltage)*.3(Length)/5E9(E0)/.03(Gap)= 4E-5(rad)

D1KA      :  DRIFT, L=0.05

KICK_ON   := 0

BKM       :  HKICK, L=0.3, KICK=-4E-5*KICK_ON
BKP       :  HKICK, L=0.3, KICK= 4E-5*KICK_ON

! Septum data from APS storage ring
! Thin septum:   2mm thickness. Peak field 0.73 T
! Thick septum: 30mm thickness. Peak field 1.08 T

DINJ1     :  DRIFT, L=3.609
DINJ2     :  DRIFT, L=3.468

BSPI1     :  HKICK, L=1, KICK=0.0436*KICK_ON
BSPI2     :  HKICK, L=1, KICK=0.0645*KICK_ON
BSPE1     :  HKICK, L=1, KICK=0.0436*KICK_ON
BSPE2     :  HKICK, L=1, KICK=0.0645*KICK_ON

QFINJ1    :  QUADRUPOLE, L=0.3,  K1= 5.617747E-01
QFINJ2    :  QUADRUPOLE, L=0.3,  K1= 9.39178128E-01
QDINJ1    :  QUADRUPOLE, L=0.3,  K1=-7.24848632E-01

D1K       :  DRIFT, L= 0.1
DINJ3     :  DRIFT, L= 1.08
DINJ4     :  DRIFT, L= 6.352
DINJ5     :  DRIFT, L=10.35
DINJ6     :  DRIFT, L= 6.08
DINJ7     :  DRIFT, L= 0.5
DINJ8     :  DRIFT, L=17

QFINJ3    :  QUADRUPOLE, L=0.3,  K1= 0.303654402749465
QFINJ4    :  QUADRUPOLE, L=0.3,  K1= 0.277992654197936
QDINJ2    :  QUADRUPOLE, L=0.3,  K1=-0.388273669795229


!--------1---------2---------3---------4---------5---------6---------7--------8
! Ring Beamlines
!--------1---------2---------3---------4---------5---------6---------7--------8

!                             !
!-------- Level 0 ------------!
!                             !
FD1       :  LINE=(DLA1, SF,  DSF, QFA,   DLA2, QDA,   DSD, SD,   DLA3)
DF1       :  LINE=(DLA3, SD1, DSD, QDMA1, DLA2, QFMA1, DSF, SF1,  DLS )

FD2       :  LINE=( DLMS1, QFMS1, DLMS2, QDMS1, &
                    DLMS3, QFMS2, DLMS4, QDMS2, &
                    DLMS5)

FD4       :  LINE=(DLMT1, QFMT1, DLMT2, QDMT1, &
                   DLMT3, QFMA2, DLMA1, QDMA2, &
                   DLMA2, QFMA3, DLMA3, QDMA3, &
                   DLMA4, QFMT2, DLMT4, QDMT2, &
                   DLMT5)

FD5       :  LINE=( DMI1, QFMINJ1, DMI2, QDMINJ1, &
                    DMI3, QFMINJ2, DMI4, QDMINJ2, &
                    DMI5, QFMINJ3, DMI6, QDMINJ3, &
                    DMI6, QFMINJ3, DMI6, QDMINJ3, &
                    DMI6, QFMINJ3, DMI6, QDMINJ3, &
                    DMI6  )

SCELL0    :  LINE=(DLI1H, QFI, DLI1, QDI, DLI1H)

RFCELL    :  LINE=(DRFH, QFW, DRF, RFC, 2*DRF, RFC, DRF, QDRF, &
                              DRF, RFC, 2*DRF, RFC, DRF, QFRF, &
                              DRF, RFC, 2*DRF, RFC, DRF, QDRF, &
                              DRF, RFC, 2*DRF, RFC, DRFH)

KINJM     :  LINE=(D1KA, BKM, D1KA)
KINJP     :  LINE=(D1KA, BKP, D1KA)

CINJ      :  LINE=(QFINJ1, DINJ1, QDINJ1,        &
                           DINJ2, QFINJ2, DINJ2, &
                   QDINJ1, DINJ1, QFINJ1 )

INJF      :  LINE=(QFINJ4 , 3*DINJ7, &
                   BSPI2,     DINJ7, &
                   BSPI1,     DINJ5, &
                   QDINJ2,    DINJ4, &
                   QFINJ3,    DINJ6)

EXTB      :  LINE=(QFINJ4,  3*DINJ7, &
                   BSPE2,     DINJ7, &
                   BSPE1,     DINJ5, &
                   QDINJ2,    DINJ4, &
                   QFINJ3,    DINJ3)

!                             !
!-------- Level 1 ------------!
!                             !
CELL      :  LINE=( FD1, BA, -FD1 )

WIGSEC    :  LINE=( BWPH,D3W,             BWM,D2WH,           &
                          5*(D2WH,BWP,D2W,BWM,D2WH),          &
                             D2WH,BWP,               D3W,BWMH )

SUPPRESSOR:  LINE=( FD1, DLB, DF1, BA2 )
                  
!                             !
!-------- Level 2 ------------!
!                             !
ARC       :  LINE=( 19*CELL )

CWIG      :  LINE=( DRFH, QFW, D1W,  WIGSEC, D1W, &
                          QDW, D1W, -WIGSEC, D1WM )

INJ       :  LINE=( INJF,   7*KINJP, 14*D1K, CINJ, &
                  28*D1K,   8*KINJM, 10*D1K, CINJ, &
                  20*D1K,  12*KINJP,  2*D1K, CINJ, &
                   4*D1K,  15*KINJM,  6*D1K, CINJ, &
                   4*D1K,  22*KINJP,  -EXTB        )

UTIL      :  LINE=( 12*SCELL0, DLI1H, QFI, DLI1H )


!                             !
!-------- Level 3 ------------!
!                             !
WIGLS             : LINE=( RFCELL, 10*CWIG, DRFH, QFW, DRFH )

INJECTION_STRAIGHT: LINE=( SUPPRESSOR, FD5, INJ,   -FD5, -SUPPRESSOR )
UTILITY_STRAIGHT  : LINE=( SUPPRESSOR, FD2, UTIL,  -FD2, -SUPPRESSOR )

!                             !
!-------- Level 4 ------------!
!                             !
WIGGLER_STRAIGHT  : LINE=( SUPPRESSOR, FD4, WIGLS, -FD4, -SUPPRESSOR )


!                             !
!-------- Level 5 ------------!
!                             !
RING      :  LINE=( 2*( ARC, WIGGLER_STRAIGHT ), &
                        ARC, INJECTION_STRAIGHT, &
                    2*( ARC, WIGGLER_STRAIGHT ), &
                        ARC, UTILITY_STRAIGHT    )
 

BEAM, PARTICLE=POSITRON, PC=5.066
